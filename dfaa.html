<!DOCTYPE html>
<HTML>

<HEAD>

  <title>Alexandr Poltavsky, software developer</title>

  <meta http-equiv="Content-Type" content="text/html;charset=utf-8" />
  <meta http-equiv="Cache-Control" content="no-store" />

  <link rel="stylesheet" type="text/css" href="css/common.css" />
  <link rel="stylesheet" href="highlight/styles/hybrid.css">

</HEAD>

<BODY> 

<div id="wrap">

  <!-- header part -->

  <div id="header">

    <a href="/energia-buran.html">
      <img width="160" src="images/alexandr-poltavsky-avatar.jpeg" title="Energia-Buran Military Space System" align="left"/>
    </a>
    <strong>
      Personal blog of Alexandr Poltavsky. <br/> 
      Software developer. <em>alexpolt&#64;yandex.ru</em><br/>
    </strong>
    <ul id="menu">
      <li><a href="/">Contents</a></li>
      <li><a href="https://github.com/alexpolt/">Github</a></li>
    </ul>

  </div>


  <div id="content" style="clear: left">

  <!-- here goes the main content -->

<h2>Introducing DFAA antialiasing algorithm</h2>

<p>Aliasing is a longstanding problem in computer graphics. To date a lot of different 
  algorithms have been developed. These are just some of them: SSAA, MSAA, CSAA, MLAA, FXAA etc.</p>

<p>In graphics we have to deal with spatial aliasing. Here is a depiction (sorry for quality):</p>

<p><center><img src="images/spatial-aliasing.png" alt="" title="" /></center></p>

<p>You can see from the second and third examples in the figure that having proper pixel color 
  can provide important information to our brain. This becomes even more crucial in VR.
  Thinking about aliasing in that direction got me to an interesting idea: what 
  if we somehow store the geometry information of the polygon and reuse it in a pixel shader?</p>

<p>After iterating through a number of options (store edge equations in some form) I
  realized that all we need is already available to us: derivatives coupled with Barycentric
  coordinates. I have already been toying with the [0,0], [1,0], [0,1] coordinate system 
  in the past, and I know other people also used it for things like wireframe rendering.</p>

<p>So, the first step in the DFAA algorithm is to assign the Barycentric coordinate system
  to the polygon. For non-indexed geometry this is done with the vertex id (or you can always 
  provide uv's for that). For HLSL SM3.0 the code looks like this:</p>

<pre><code>//Barycentrics
static float2 uv01[] = { {0,0}, {1,0}, {0,1} };
...
//No SV_VertexID in SM3.0 , so get vertex id from buffer, is should be sequential
out.uv01 = uv01[ 3 * frac(vertex_id/3) ];
</code></pre>

<p>For indexed geometry SV_VertexID won't work (unless your vertices are in strips), so we need
  to either use a geometry shader or generate a vertex id buffer. I provide a function template
  for this. You can use it to generate the vertex ids at runtime like this:</p>

<pre><code>auto vertex_id_buffer = generate_vertex_ids&lt;float&gt;( index_buffer, tris_count/3 );
</code></pre>

<p>Here is a sample how it works: <a href="http://ideone.com/iiB3xw">ideone</a>. The code: <a href="https://github.com/alexpolt/DFAA/blob/master/generate-vertex-ids.h">Github</a> <br />
  You may end up having some number of triangles with wrong uv01 mapping (if there are loops in the 
  mesh), but that is not critical to DFAA (no or partial AA there).</p>

<p>The <strong>pixel shader part</strong> is done in two steps. In the first step we use the provided Barycentrics 
  (that should be used with <em>noperspective</em> modifier) and compute two values: direction of sampling 
  and coverage. Picture here will make it easy for you to understand the code of the shader.</p>

<p><center><img src="images/dfaa-algorithm.png" alt="" title="" /></center></p>

<p>Here is the first pass shader function:</p>

<pre><code>static const float pi2 = 2*3.1415926;


//Implementation of the DFAA algorithm
//should be fed with a [0,0],[1,0],[0,1] UV
//returns one byte with packed direction and coverage


static float rad = 0.5; //rad - radius of sampling, 0.5 means half-pixel
static float steps = 3; //(steps+1)^2 - total number subsamples for coverage computation

//uv01 should be with 'noperspective' modifier (though it seems to have no impact)

float DFAA( float2 uv01 ) {

  float2 uvdx = ddx( uv01 );
  float2 uvdy = ddy( uv01 );

  float area=0;

  //compute non-coverage

  for(float y=0; y&lt;=steps; y++) {
    for(float x=0; x&lt;=steps; x++) {
      float2 dxdy = float2( 2*rad*x/steps - rad, 2*rad*y/steps - rad );
      float2 edge = uv01 + uvdx * dxdy.x  + uvdy * dxdy.y;
      // if we are out of the triangle - increase by one the non-coverage
      if( edge.x &lt; 0 || edge.y &lt; 0 || ( edge.x + edge.y ) &gt; 1 ) area++;
    } 
  }

  //get actual coverage
  area = 1 - area / pow(steps+1, 2);

  //the next big step is to compute the direction of sampling
  //we get the inverse matrix and compute the edge vectors of the polygon
  //then we break the polygon into three independent parts by medians
  //and get the right edge, then rotate it by 90 degrees

  float2 dir;
  float2 uvdx2 = normalize( uvdx ), uvdy2 = normalize( uvdy );

  //matrix inverse
  float det = 1 / ( uvdx2.x * uvdy2.y - uvdx2.y * uvdy2.x );
  float2 xydu = det * float2( uvdy2.y, -uvdx2.y );
  float2 xydv = det * float2( -uvdy2.x, uvdx2.x );

  //choosing the edge using triangle medians
  float2 z = float2( xydv - xydu );
  if( uv01.y &gt; uv01.x &amp;&amp; (uv01.y+2*uv01.x) &lt; 1 ) dir = float2( -xydv.y, xydv.x );
  else if( uv01.x &gt; uv01.y &amp;&amp; (uv01.x+2*uv01.y) &lt; 1 ) dir = float2( xydu.y, -xydu.x );
  else dir = float2( z.y, -z.x );

  //so that we don't have to worry about winding of uv's
  dir = cross( float3(xydu, 0), float3(xydv, 0) ).z &gt; 0 ? dir : -dir;

  //encode direction as an angle
  float dir_atan = atan2( dir.y, dir.x );
  float dir_angle = ( dir_atan &lt; 0 ? dir_atan + pi2 : dir_atan ) / pi2;

  //pack into one byte
  float dfaa =  ( floor(15*dir_angle)*16 + 15*area ) / 255;

  return dfaa;

}
</code></pre>

<p>As you see, all we need is <strong>1 byte</strong> of information per pixel. That's the cost of this method.
  After getting that information in a framebuffer we need to make a fullscreen pass to perform
  antialiasing. We do that by lerping between two colors: current and the one sampled along 
  the computed direction and using computed coverage info.</p>

<pre><code>//pixel to display and packed DFAA in alpha
sampler tex0 : register(s0); 

static const float pi2 = 2*3.1415926;

//defines used by DFAA
#define dfaa_screen_texture tex0
#define dfaa_tex2D tex2D

//DFAA: performs fetch, unpack and lerp
float3 DFAA( float2 screenxy ) {

  float4 color0 = dfaa_tex2D( dfaa_screen_texture, screenxy );
  float dfaa_packed = color0.a;

  //unpack
  float dir_angle = floor(255*dfaa_packed/16)/15;
  float2 dir = float2( cos( dir_angle * pi2 ), sin( dir_angle * pi2 ) );
  float area = frac(255*dfaa_packed/16)*16/15;

  float2 sdx = ddx( screenxy );
  float2 sdy = ddy( screenxy );

  float4 color1 = color0;

  //only do fetch if necessary (edge pixels)
  if( area &gt; 0 &amp;&amp; area &lt; 1 ) 
    color1 = dfaa_tex2D( dfaa_screen_texture, screenxy + sdx*dir.x + sdy*dir.y );

  return lerp( color1, color0, area ).rgb;
}
</code></pre>

<p>So that's it. I provide a <a href="https://github.com/alexpolt/DFAA">GitHub repository</a> with the two pixel 
  shaders. I also put there a small Windows demo and a RenderMonkey project. In the RenderMonkey
  project you have convenient knobs on the right to turn on/off DFAA and see the difference.</p>

<p>Here is a zoomed in screenshot of the DFAA at work.</p>

<p><center><img src="images/dfaa-demo.png" alt="" title="DFAA at work" /></center> </p>

<p>It seems like that approach could also help improve shadow quality. Would be nice to see DFAA 
  being used in real games.</p>


<script src="highlight/highlight.pack.js"></script>
<script>
  //hljs.configure({languages: ["C++"]});
  hljs.initHighlightingOnLoad();
</script>


<div id="disqus_thread"></div>
<script>
    /**
     *  RECOMMENDED CONFIGURATION VARIABLES: EDIT AND UNCOMMENT THE SECTION BELOW TO INSERT DYNAMIC VALUES FROM YOUR PLATFORM OR CMS.
     *  LEARN WHY DEFINING THESE VARIABLES IS IMPORTANT: https://disqus.com/admin/universalcode/#configuration-variables
     */
    /*
    var disqus_config = function () {
        this.page.url = PAGE_URL;  // Replace PAGE_URL with your page's canonical URL variable
        this.page.identifier = PAGE_IDENTIFIER; // Replace PAGE_IDENTIFIER with your page's unique identifier variable
    };
    */
    if( ! document.getElementById("contents") ) // alexpolt: exclude main page with contents
    (function() {  // DON'T EDIT BELOW THIS LINE
        var d = document, s = d.createElement('script');
        
        s.src = '//alexpolt-github-io.disqus.com/embed.js';
        
        s.setAttribute('data-timestamp', +new Date());
        (d.head || d.body).appendChild(s);
    })();
</script>
<noscript>Please enable JavaScript to view the <a href="https://disqus.com/?ref_noscript" rel="nofollow">comments powered by Disqus.</a></noscript>



  </div> <!-- end content -->

</div> <!-- end wrap -->



<!-- Google Analytics -->
<script>
  (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
  (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
  m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
  })(window,document,'script','//www.google-analytics.com/analytics.js','ga');

  ga('create', 'UA-75341409-1', 'auto');
  ga('send', 'pageview');
</script>

</BODY>

